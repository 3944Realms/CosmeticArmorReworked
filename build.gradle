buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

import net.minecraftforge.gradle.common.task.SignJar

import java.nio.file.Files

version = "1.16.5-v1a"
group = "lain.mods.cos"
archivesBaseName = "CosmeticArmorReworked"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    accessTransformer = file("$rootDir/src/main/resources/META-INF/accesstransformer.cfg")
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.0.0'
}

processResources {
    from 'LICENSE.txt'
}

jar {
    manifest {
        attributes(["Specification-Title"     : "CosmeticArmorReworked",
                    "Specification-Vendor"    : "zlainsama",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "zlainsama",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task apiZip(type: Jar) {
    from(sourceSets.main.java) {
        include "lain/mods/cos/api/**"
    }
    from(sourceSets.main.output) {
        include "lain/mods/cos/api/**"
    }

    classifier = 'api'
}

artifacts {
    archives apiZip
}

jar.finalizedBy('reobfJar')

task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        System.getenv('SIGNJAR_KEYSTORE') != null
    }

    if (System.getenv('SIGNJAR_KEYSTORE') != null) {
        keyStore = Files.createTempFile(getTemporaryDir().toPath(), 'keystore-', '.jks').toString()
        file(keyStore).withOutputStream { output ->
            output.write(Base64.getDecoder().decode(System.getenv('SIGNJAR_KEYSTORE')))
        }
        alias = System.getenv('SIGNJAR_ALIAS')
        storePass = System.getenv('SIGNJAR_STOREPASS')
        keyPass = System.getenv('SIGNJAR_KEYPASS')
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }

    doLast {
        delete keyStore
    }
}

build.dependsOn signJar
