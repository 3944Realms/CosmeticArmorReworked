plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '5.+'
}

import net.minecraftforge.gradle.common.tasks.SignJar

import java.nio.file.Files

version = "1.19-v1"
group = "lain.mods.cos"
archivesBaseName = "CosmeticArmorReworked"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.19'
    accessTransformer = file("$rootDir/src/main/resources/META-INF/accesstransformer.cfg")
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.19-41.0.1'
}

processResources {
    from 'LICENSE.txt'
}

jar {
    manifest {
        attributes(["Specification-Title"     : "CosmeticArmorReworked",
                    "Specification-Vendor"    : "zlainsama",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "zlainsama",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task apiZip(type: Jar) {
    from(sourceSets.main.java) {
        include "lain/mods/cos/api/**"
    }
    from(sourceSets.main.output) {
        include "lain/mods/cos/api/**"
    }

    classifier = 'api'
}

artifacts {
    archives apiZip
}

jar.finalizedBy('reobfJar')

task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        System.getenv('SIGNJAR_KEYSTORE') != null
    }

    if (System.getenv('SIGNJAR_KEYSTORE') != null) {
        keyStore = Files.createTempFile(getTemporaryDir().toPath(), 'keystore-', '.jks').toString()
        file(keyStore).withOutputStream { output ->
            output.write(Base64.getDecoder().decode(System.getenv('SIGNJAR_KEYSTORE')))
        }
        alias = System.getenv('SIGNJAR_ALIAS')
        storePass = System.getenv('SIGNJAR_STOREPASS')
        keyPass = System.getenv('SIGNJAR_KEYPASS')
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }

    doLast {
        delete keyStore
    }
}

build.dependsOn signJar
